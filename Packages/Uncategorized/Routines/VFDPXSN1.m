VFDPXSN1 ;CFS - Set the SNOMED nodes. ;05/15/2013
 ;;2011.1.3;DSS,INC VXVISTA OPEN SOURCE;**1**;08 Aug 2013
 ;Copyright 1995-2013,Document Storage Systems Inc. All Rights Reserved
 ;
 ;NOTE:
 ;	For future enhancements, this routine can be copied, pasted and renamed.
 ;  Then modification can be made to fit the vxVistA CPRS tab.
 ;
 ;	Other routines that can be copied, pasted and renamed, or looked at for
 ;  examples are:
 ;		PXAIPOV
 ;		PXAICPT
 ;		PXAIPED
 ;		PXAIXAM
 ;		PXAIHF
 ;		PXAIIMM
 ;		PXAISK
 ;
 Q
 ;
VFDSNO ;Main entry point.
 ;
 K PXAERR
 S PXAERR(8)=PXAK
 S PXAERR(7)="VFDSNO"
 ;
 ;Create PXAA array
 N IND,PXAA
 S IND=""
 F  S IND=$O(@PXADATA@("VFDSNO",PXAK,IND)) Q:IND=""  D
 . S PXAA(IND)=@PXADATA@("VFDSNO",PXAK,IND)
 ;
 ;Validate the data.
 N STOP
 D VAL^VFDPXSN2
 I $G(STOP) Q
 ;
SETVARA ;Set the after visit variables for SNOMED codes.
 ;Data sent from CPRS.
 N AFTER0,AFTER12,AFTER802,AFTER811,AFTER812
 S $P(AFTER0,U,1)=$G(PXAA("VFDSNO CODE"))
 I $G(PXAA("DELETE")) S $P(AFTER0,U,1)="@"
 S $P(AFTER0,U,2)=$G(PATIENT)
 S $P(AFTER0,U,3)=$G(PXAVISIT)
 ;--PROVIDER NARRATIVE
 S $P(AFTER0,U,4)=$G(PXAA("NARRATIVE")) D
 .I $G(PXAA("NARRATIVE"))]"" S $P(AFTER0,"^",4)=+$$PROVNARR^VFDPXPE($G(PXAA("NARRATIVE")),21640.01)
 I $P(AFTER0,"^",4)<0 D VAL04^VFDPXSN2,ERR^VFDPXPE Q:$D(STOP)
 S $P(AFTER0,U,5)=$G(PXAA("USAGE"))
 S $P(AFTER12,U,1)=$G(PXAA("EVENT D/T"))
 S $P(AFTER12,U,4)=$G(PXAA("ENC PROVIDER"))
 S $P(AFTER811,U,1)=$G(PXAA("COMMENT"))
 ;--PROVIDER NARRATIVE CATEGORY
 S $P(AFTER802,"^",1)=$G(PXAA("CATEGORY"))
 I $G(PXAA("CATEGORY"))]"" S $P(AFTER802,"^",1)=+$$PROVNARR^VFDPXPE($G(PXAA("CATEGORY")),21640.01)
 I $P(AFTER802,"^",1)<0 D VAL45^VFDPXSN2,ERR^VFDPXPE Q:$D(STOP)
 ;--PACKAGE AND SOURCE
 S $P(AFTER812,"^",2)=$G(PXAPKG)
 S $P(AFTER812,"^",3)=$G(PXASOURC)
 ;
 S ^TMP("PXK",$J,"VFDPXSN4",PXAK,0,"AFTER")=AFTER0
 S ^TMP("PXK",$J,"VFDPXSN4",PXAK,12,"AFTER")=AFTER12
 S ^TMP("PXK",$J,"VFDPXSN4",PXAK,802,"AFTER")=AFTER802
 S ^TMP("PXK",$J,"VFDPXSN4",PXAK,811,"AFTER")=AFTER811
 S ^TMP("PXK",$J,"VFDPXSN4",PXAK,812,"AFTER")=AFTER812
 ;
SETVARB ;Set the before visit variables for SNOMED codes.
 ;Pull data from ^VFD(21640.01 for visit.
 N BEFOR0,BEFOR12,BEFOR802,BEFOR811,BEFOR812
 N IENB,PXAAX,PXBCNT,PXBKY,PXBSKY,PXBSAM
 S PXBCNT=""
 D VFDSNO^VFDPXSN3(PXAVISIT,.PXBCNT)
 ;
 S IENB=""
 I PXBCNT>0 D
 . S PXAAX("VFDSNO CODE")=PXAA("VFDSNO CODE")
 . S IENB=$O(PXBKY(PXAAX("VFDSNO CODE"),IENB))
 I $G(IENB) D
 . S BEFOR0=$G(^VFD(21640.01,IENB,0))
 . S BEFOR12=$G(^VFD(21640.01,IENB,12))
 . S BEFOR802=$G(^VFD(21640.01,IENB,802))
 . S BEFOR811=$G(^VFD(21640.01,IENB,811))
 . S BEFOR812=$G(^VFD(21640.01,IENB,812))
 E  S (BEFOR0,BEFOR11,BEFOR12,BEFOR802,BEFOR811,BEFOR812)=""
 ;
 S ^TMP("PXK",$J,"VFDPXSN4",PXAK,0,"BEFORE")=BEFOR0
 S ^TMP("PXK",$J,"VFDPXSN4",PXAK,12,"BEFORE")=BEFOR12
 S ^TMP("PXK",$J,"VFDPXSN4",PXAK,802,"BEFORE")=BEFOR802
 S ^TMP("PXK",$J,"VFDPXSN4",PXAK,811,"BEFORE")=BEFOR811
 S ^TMP("PXK",$J,"VFDPXSN4",PXAK,812,"BEFORE")=BEFOR812
 S ^TMP("PXK",$J,"VFDPXSN4",PXAK,"IEN")=IENB
 ;
 Q
